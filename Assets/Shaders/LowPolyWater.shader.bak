Shader "Custom/LowPolyWater"
{
    Properties
    {
        _MainTex("Texture", 2D) = "white" {}
        _Color("Color", Color) = (1, 1, 1, 1)
    }
    SubShader
    {
        Tags{ "RenderType" = "Opaque" }
        LOD 200

        Pass
        {
            CGPROGRAM
            #pragma vertex vert
            #pragma fragment frag
            #pragma geometry geom

            #include "UnityCG.cginc"

            struct VS_Input
            {
                float4 vertex : POSITION;
                float3 normal : NORMAL;
                float2 uv : TEXCOORD0;
            };

            struct VS_Output
            {
                float4 vertex : SV_POSITION;
                float3 normal : NORMAL;
                float2 uv : TEXCOORD0;
                float3 worldPosition : TEXCOORD1;
            };

            static const float PI = float(3.14159);

            sampler2D _MainTex;
            float4 _MainTex_ST;
            fixed4 _Color;

            uniform float _Height;
            uniform int _Waves;
            uniform float4 _Direction[8];
            uniform float _TimeScale = 1.0;

            float Amplitude(int i)
            {
                return 0.5 / (i + 1.0f);
            }

            float WaveLength(int i)
            {
                return 8.0f * PI / (i + 1.0f);
            }

            float Speed(int i)
            {
                return 1.0 + 2.0 * i;
            }

            float Wave(int i, float x, float y)
            {
                float freq = 2.0 * PI / WaveLength(i);
                float phase = Speed(i) * freq;
                float theta = dot(_Direction[i].xy, float2(x, y));

                return Amplitude(i) * sin(theta * freq + _Time.x * _TimeScale * phase);
            }

            float WaveHeight(float x, float y)
            {
                float height = 0.0;

                for(int i = 0; i < _Waves; i++)
                {
                    height += Wave(i, x, y);
                }

                return height;
            }

            VS_Output vert(VS_Input v)
            {
                VS_Output o;
                // Water simulation
                v.vertex.y = _Height + WaveHeight(v.vertex.x, v.vertex.z);
                // v.vertex.y = _Amplitude[0];
                // Space transform
                o.vertex = mul(UNITY_MATRIX_MVP, v.vertex);
                o.uv = TRANSFORM_TEX(v.uv, _MainTex);
                o.normal = v.normal;
                o.worldPosition = mul(_Object2World, v.vertex).xyz;

                return o;
            }

            [maxvertexcount(3)]
            void geom(triangle VS_Output input[3], inout TriangleStream<VS_Output> OutputStream)
            {
                VS_Output test = (VS_Output)0;
                float3 normal = normalize(cross(input[1].worldPosition.xyz -
                                                input[0].worldPosition.xyz,
                                                input[2].worldPosition.xyz - input[0].worldPosition.xyz));

                for (int i = 0; i < 3; i++)
                {
                    test.normal = normal;
                    test.vertex = input[i].vertex;
                    test.uv = input[i].uv;
                    OutputStream.Append(test);
                }
            }

            fixed4 frag(VS_Output i) : SV_Target
            {
                // sample the texture
                fixed4 col = tex2D(_MainTex, i.uv) * _Color;

                float3 lightDir = float3(1, 1, 0);
                float ndotl = dot(i.normal, normalize(lightDir));

                return col * ndotl;
            }
            ENDCG
        }
    }
}